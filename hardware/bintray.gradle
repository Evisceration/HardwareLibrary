apply plugin: 'com.github.dcendents.android-maven'

def libraryGroupId = 'at.amartinz'
def libraryArtifactId = 'hardware'
def libraryVersion = '1.0.1'

group libraryGroupId

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId libraryGroupId
            artifactId libraryArtifactId
            version libraryVersion

            name 'hardware-library'
            description 'Get information about the device, its hardware and software'
            url 'https://github.com/Evisceration/HardwareLibrary'

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'https://opensource.org/licenses/MIT'
                }
            }
            developers {
                developer {
                    id 'evisceration'
                    name 'Alexander Martinz'
                    email 'eviscerationls@gmail.com'
                }
            }
            scm {
                connection 'https://github.com/Evisceration/HardwareLibrary.git'
                developerConnection 'git@github.com:Evisceration/HardwareLibrary.git'
                url 'https://github.com/Evisceration/HardwareLibrary'
            }
        }
    }
}

//------------------------

apply plugin: 'com.jfrog.bintray'

version libraryVersion

if (project.hasProperty("android")) {
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += configurations.compile
        failOnError false
    }
} else {
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = bintray_user
    key = bintray_api_key

    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'hardware-library'
        desc = 'Get information about the device, its hardware and software'
        websiteUrl = 'https://github.com/Evisceration/HardwareLibrary'
        vcsUrl = 'https://github.com/Evisceration/HardwareLibrary.git'
        licenses = ["MIT"]
        publicDownloadNumbers = false
        version {
            gpg {
                sign = true
            }
        }
    }
}

bintrayUpload {
    dependsOn javadocJar
    dependsOn sourcesJar
}
